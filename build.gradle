plugins {
    id "io.freefair.lombok" version "8.6" apply false
    id "com.diffplug.gradle.spotless" version "6.25.0" apply false
    id 'com.adarshr.test-logger' version '4.0.0' apply false
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.freefair.lombok"
    apply plugin: "com.diffplug.spotless"
    apply plugin: "com.adarshr.test-logger"
    java {
        sourceCompatibility = '17'
    }

    spotless {
        java {
            googleJavaFormat()
        }
    }
}

configure(subprojects.findAll {
    !["commons", "bdd"].contains(it.name)
}) {
    dependencies {
        compileOnly project(":commons")
    }
    tasks.register('copyJar', Copy) {
        from jar
        into rootProject.layout.buildDirectory
    }
}

project('bdd') {
    test {
        onlyIf {
            project.hasProperty('bddTests')
        }
    }
}