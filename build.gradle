plugins {
    id "io.freefair.lombok" version "8.6" apply false
    id "com.diffplug.gradle.spotless" version "6.25.0" apply false
    id 'com.adarshr.test-logger' version '4.0.0' apply false
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "groovy"
    apply plugin: "io.freefair.lombok"
    apply plugin: "com.diffplug.spotless"
    apply plugin: "com.adarshr.test-logger"
    java {
        sourceCompatibility = '17'
    }

    spotless {
        java {
            importOrder()
            removeUnusedImports()
            googleJavaFormat()
        }
        groovy {
            importOrder()
            removeSemicolons()
            greclipse()
            excludeJava()
        }
    }
}

configure(subprojects.findAll {
    !["commons", "bdd"].contains(it.name)
}) {
    dependencies {
        compileOnly project(":commons")
        testImplementation project(":commons")
    }
    tasks.register('copyJar', Copy) {
        from jar
        into rootProject.layout.buildDirectory
    }
}

configure(subprojects.findAll {
    it.name != "bdd"
}) {
    dependencies {
        testImplementation 'org.apache.groovy:groovy-all:4.0.21'
        testImplementation platform('org.spockframework:spock-bom:2.4-M4-groovy-4.0')
        testImplementation 'org.spockframework:spock-core'
        testImplementation "org.testcontainers:localstack:1.19.8"
    }
    test {
        useJUnitPlatform()
    }
}

project('bdd') {
    test {
        onlyIf {
            project.hasProperty('bddTests')
        }
    }
}